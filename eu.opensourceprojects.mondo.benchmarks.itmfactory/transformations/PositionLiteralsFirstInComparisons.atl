-- @path JAVA=/code-quality-check/metamodels/java.ecore
-- @path CHK=/code-quality-check/metamodels/anomalies.ecore

module PositionLiteralsFirstInComparisons;
create OUT : CHK from IN : JAVA;

helper context OclAny def : getAbstractMethodDeclaration : JAVA!AbstractMethodDeclaration =
	if self.oclIsKindOf(JAVA!AbstractMethodDeclaration) then
		self
	else
		if self.oclIsTypeOf(JAVA!Model) then
			OclUndefined
		else
			self.refImmediateComposite().getAbstractMethodDeclaration
		endif
	endif;

helper context JAVA!MethodInvocation def : checkPositionLiteralsFirstInComparisons : Boolean =
	if self.method.name = 'equals' then
		if self.arguments->size() = 1 then
			if self.expression.oclIsTypeOf(JAVA!StringLiteral) then
				true
			else
				not self.arguments->first().oclIsTypeOf(JAVA!StringLiteral)
			endif
		else
			true
		endif
	else
		true
	endif;

rule MethodInvocation {
	from
		s: JAVA!MethodInvocation (not s.checkPositionLiteralsFirstInComparisons)
	to
		t: CHK!PositionLiteralsFirstInComparisons (
			description <- 'position literals first in comparisons, check method ' + s.getAbstractMethodDeclaration.name
		)
	do {
		t.trace <- Sequence{s};
	}
}

endpoint rule EndRule() {
	do {
		'TRANSFORMATION'.debug('END');
	}
}
