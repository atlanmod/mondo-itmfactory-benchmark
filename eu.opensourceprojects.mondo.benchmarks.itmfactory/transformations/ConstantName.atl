-- @path JAVA=/code-quality-check/metamodels/java.ecore
-- @path CHK=/code-quality-check/metamodels/anomalies.ecore

module ConstantName;
create OUT : CHK from IN : JAVA;

helper def : FORMAT : String = '^[A-Z][A-Z0-9]*(_[A-Z0-9]+)*$';

helper context OclAny def : getFieldDeclaration : OclAny =
	if self.oclIsTypeOf(JAVA!FieldDeclaration) then
		self
	else
		if self.oclIsTypeOf(JAVA!Model) then
			OclUndefined
		else
			self.refImmediateComposite().getFieldDeclaration
		endif
	endif;

helper context OclAny def : isInFieldDeclaration : Boolean =
	if self.oclIsTypeOf(JAVA!FieldDeclaration) then
		true
	else
		if self.oclIsTypeOf(JAVA!Model) then
			false
		else
			self.refImmediateComposite().isInFieldDeclaration
		endif
	endif;

helper context JAVA!Modifier def : isFinal : Boolean =
	if not self.inheritance.oclIsUndefined() then
		self.inheritance.toString() = 'final'
	else
		false
	endif;

helper context JAVA!FieldDeclaration def : isStatic : Boolean =
	if not self.modifier.oclIsUndefined() then
		self.modifier.static		
	else
		false
	endif;

helper context JAVA!FieldDeclaration def : isFinal : Boolean =
	if not self.modifier.oclIsUndefined() then
		self.modifier.isFinal
	else
		false
	endif;

helper context JAVA!VariableDeclaration def : checkConstantName : Boolean =
	if self.isInFieldDeclaration then
		let container : JAVA!FieldDeclaration = self.getFieldDeclaration in
		if (container.isStatic and container.isFinal) or self.oclIsTypeOf(JAVA!EnumConstantDeclaration) then
			self.name.regexReplaceAll(thisModule.FORMAT, '@').toSequence().count('@') >= 1
		else
			true
		endif
	else
		true
	endif;

rule ConstantName {
	from
		s: JAVA!VariableDeclaration (not s.proxy and not s.checkConstantName)
	to
		t: CHK!ConstantName (
			description <- 'constant field ' + s.name + ' does not follow the format ' + thisModule.FORMAT + ' for constants'
		)
	do {
		t.trace <- Sequence{s};	
	}
}


endpoint rule EndRule() {
	do {
		'TRANSFORMATION'.debug('END');
	}
}