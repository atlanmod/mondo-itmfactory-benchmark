-- @path JAVA=/code-quality-check/metamodels/java.ecore
-- @path CHK=/code-quality-check/metamodels/anomalies.ecore

module StaticVariableName;
create OUT : CHK from IN : JAVA;

helper def : FORMAT : String = '^[a-z][a-zA-Z0-9]*$';

helper context OclAny def : getFieldDeclaration : OclAny =
	if self.oclIsTypeOf(JAVA!FieldDeclaration) then
		self
	else
		if self.oclIsTypeOf(JAVA!Model) then
			OclUndefined
		else
			self.refImmediateComposite().getFieldDeclaration
		endif
	endif;

helper context OclAny def : isInFieldDeclaration : Boolean =
	if self.oclIsTypeOf(JAVA!FieldDeclaration) then
		true
	else
		if self.oclIsTypeOf(JAVA!Model) then
			false
		else
			self.refImmediateComposite().isInFieldDeclaration
		endif
	endif;

helper context JAVA!Modifier def : isFinal : Boolean =
	if not self.inheritance.oclIsUndefined() then
		self.inheritance.toString() = 'final'
	else
		false
	endif;

helper context JAVA!FieldDeclaration def : isStatic : Boolean =
	if not self.modifier.oclIsUndefined() then
		self.modifier.static		
	else
		false
	endif;

helper context JAVA!FieldDeclaration def : isFinal : Boolean =
	if not self.modifier.oclIsUndefined() then
		self.modifier.isFinal
	else
		false
	endif;

helper context JAVA!VariableDeclaration def : checkStaticVariableName : Boolean =
	if self.isInFieldDeclaration then
		let container : JAVA!FieldDeclaration = self.getFieldDeclaration in
		if (container.isStatic and not container.isFinal) or self.oclIsTypeOf(JAVA!EnumConstantDeclaration) then
			self.name.regexReplaceAll(thisModule.FORMAT, '@').toSequence().count('@') >= 1
		else
			true
		endif
	else
		true
	endif;

rule StaticVariableName {
	from
		s: JAVA!VariableDeclaration (not s.proxy and not s.checkStaticVariableName)
	to
		t: CHK!StaticVariableName (
			description <- 'static field ' + s.name + ' does not follow the format ' + thisModule.FORMAT + ' for static variables'
		)
	do {
		t.trace <- Sequence{s};	
	}
}


endpoint rule EndRule() {
	do {
		'TRANSFORMATION'.debug('END');
	}
}

